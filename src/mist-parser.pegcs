/**
 *
 *     _|      _|  _|              _|
 *     _|_|  _|_|        _|_|_|  _|_|_|_|
 *     _|  _|  _|  _|  _|_|        _|
 *     _|      _|  _|      _|_|    _|
 *     _|      _|  _|  _|_|_|        _|_|
 *
 *             MIST BUILD SYSTEM
 * Copyright (c) 2015 On Demand Solutions, inc.
 *       Released under the MIT License
 */

mistfile
  = _? statements:(s:statement _? {s})+
    {statements}
  ;

statement
  = statement_rule
  / statement_vardecl
  ;

statement_rule
  = ':'
    foreach:(_? "foreach")?
    main_inputs:(_? i:glob_list {i})?
    dep_inputs:(_? '|' _? i:glob_list {i})?
    order_inputs:(_? '||' _? i:glob_list {i})?
    command:(_? cmd:rule_command {cmd})
    main_outputs:(_? i:delim_path_list {i})?
    aux_outputs:(_? '|' _? i:delim_path_list {i})?
    ___* _
    {
      type: 'rule'
      foreach: (if foreach? then yes else no)
      main_inputs: main_inputs || []
      dep_inputs: dep_inputs || []
      order_inputs: order_inputs || []
      command: command
      main_outputs: main_outputs || []
      aux_outputs: aux_outputs || []
    }
  ;

rule_command
  = '|>' _? cmd:((!(_? '|>')) c:substitute_itr {c})+ _? '|>'
    {cmd.join ''}
  ;

delim_path_list
  = first:delim_path others:(_ d:delim_path {d})*
    {[first].concat (others || [])}
  ;

delim_path
  = $[a-z0-9\/\.\-\_\~\%]i+
  ;

glob_list
  = first:glob others:(_ g:glob {g})* _
    {[first].concat (others || [])}
  ;

glob
  = $(glob_basic_char $(glob_basic_char/'|')*)+
  ;

glob_basic_char
  = [a-z0-9\/\*\.\-\_\~\{\}\(\)]i
  ;

statement_vardecl
  = name:variable_name _? '=' _? val:substitute_string
    {
      type: 'var'
      name: name
      val: val
    }
  ;

substitute_string
  = str:substitute_itr+
    {str.join ''}
  ;

substitute_itr
  = substitute_escape
  / [^\r\n]
  ;

substitute_escape
  = ('$(' _? name:variable_name _? ')') {'$' + name}
  ;

variable_ref "variable reference"
  = '$(' _? name:variable_name _? ')'
    {'${' + name + '}'}
    /*'//synhi fix*/
  ;

variable_name
  = $([a-z0-9_]i+)
  ;

_ ""
  = __+
  ;

__ ""
  = ___
  / [\r\n]
  / comment
  ;

___ ""
  = [ \t]
  ;

comment ""
  = '#' [^\r\n]*
  ;

